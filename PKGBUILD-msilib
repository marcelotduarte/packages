# Maintainer: Marcelo Duarte @marcelotduarte
# makepkg-mingw -sCLf -p PKGBUILD-msilib

_realname=python-msilib
_pyname=${_realname/-/_}
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.1
pkgrel=1
pkgdesc="Read and write Microsoft Installer files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/marcelotduarte/python-msilib/"
msys2_repository_url='https://github.com/marcelotduarte/python-msilib'
msys2_references=(
  'purl: pkg:pypi/python-msilib'
)
license=('spdx:PSF-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-ca-certificates"
  "${MINGW_PACKAGE_PREFIX}-python"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-tools"
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-pip"
  "${MINGW_PACKAGE_PREFIX}-python-coverage"
  "${MINGW_PACKAGE_PREFIX}-python-pytest"
)
options=(!strip)
if [ "$CI" == "true" ]; then
  pkgver=$(find ${_pyname}-*.tar.gz | awk -F- '{print $2}' | awk -F.tar.gz '{print $1}')
  source=("file://${startdir}/${_pyname}-${pkgver}.tar.gz")
  sha256sums=(SKIP)
else
  source=()
  sha256sums=()
fi

prepare() {
  if ! [ "$CI" == "true" ]; then
    # Local
    cd ../../${_realname}
    pkgver=$(grep -m1 "^version = " pyproject.toml | awk -F\" '{print $2}')
    python -m build -s -x -n -o "${startdir}"
    cd "${srcdir}"
    echo "Extract tar archive"
    bsdtar -x -v -f "${startdir}/${_pyname}-${pkgver}.tar.gz"
  fi

  rm -Rf "${srcdir}/${_realname}-${MSYSTEM}"
  cp -a "${srcdir}/${_pyname}-${pkgver}" "${srcdir}/${_realname}-${MSYSTEM}"
}

pkgver() {
  cd "${_realname}-${MSYSTEM}"
  grep -m1 "^version = " pyproject.toml | awk -F\" '{print $2}'
}

build() {
  cd "${_realname}-${MSYSTEM}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_realname}-${MSYSTEM}"
  pip install ${_realname} -f dist --no-deps --no-index

  mkdir -p "${srcdir}/${_realname}-test"
  cp pyproject.toml "${srcdir}/${_realname}-test/"
  cp -a tests "${srcdir}/${_realname}-test/tests/"

  cd "${srcdir}/${_realname}-test"
  coverage run
  coverage combine
  coverage report
  pip uninstall ${_realname} -y
}

package() {
  cd "${_realname}-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix="${MINGW_PREFIX}" --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
